buildscript {
	repositories {
        mavenCentral()
        maven { url 'https://cdn.fox2code.com/maven' }
        maven { url 'https://jitpack.io/' }
    }
    dependencies {
        classpath('com.fox2code.FoxLoader:dev:1.2.46')
    }
}

apply plugin: 'foxloader.dev'
apply plugin: 'maven-publish'

version '0.2'

configurations {
    shadowDope
    serverImplementation.extendsFrom(shadowDope)
}

dependencies {
    shadowDope('com.h2database:h2:2.2.220')
}

foxloader {
    // forceReload = true
    modId = "authon"
    modName = "AuthOn"
    modVersion = project.version
    modWebsite = "https://codeberg.org/tracystacktrace/authon"
    modDesc = "A simple auth mod for FoxLoader servers"
    serverMod = "net.tracyex0.authon.AuthonServer"
    // Development tools don't like that both client and server define
    // the same classes, so uncomment to disable the side inclusion.
    includeClient = false
    // includeServer = false
}

jar {
    doFirst {
        from configurations.shadowDope.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/tracystacktrace/authon")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
